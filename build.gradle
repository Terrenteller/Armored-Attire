plugins {
    id 'java'
}

group = 'com.riintouge'
def spigotVersion = "1.21.1-R0.1-SNAPSHOT"
def minecraftVersionMMU = spigotVersion.split( "-" )[0]
version = minecraftVersionMMU + "-" + new Date().format( "yyyy.MM.dd" )

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "ProtocolLib"
        url = "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        name = "NBT API"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:" + spigotVersion
    compileOnly "com.comphenix.protocol:ProtocolLib:5.3.0"
    compileOnly "de.tr7zw:item-nbt-api-plugin:2.13.1"
    implementation "org.jetbrains:annotations:24.0.0"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
